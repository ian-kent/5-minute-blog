#!/usr/bin/env perl
use Mojolicious::Lite;

use Mango;
use Mango::BSON 'bson_time';

# Documentation browser under "/perldoc"
plugin 'PODRenderer';

# Create a helper for Mango
helper 'mango' => sub {
	# Create a Mango instance once, then always use that
	state $mango = Mango->new;
	# Return the Mango instance
	return $mango;
};

# Create a GET route for /
get '/' => sub {
  # Get the current object
  my $self = shift;
  # Find all articles from the MongoDB database 'test' and collection 'articles'
  $self->mango->db('test')->collection('articles')->find({})->all(sub{
  	# Get the results
	my ($mango, $error, $docs) = @_;
	# Stash the articles for rendering
	$self->stash(articles => $docs);
	# Render the 'index' template
	$self->render('index');
  });
  # Render later, so Mango query can run
  $self->render_later;
};

# Create a POST route for /post
post '/post' => sub {
	# Get the current object
	my $self = shift;
	# Get the 'title' form parameter
	my $title = $self->param('title');
	# Get the 'content' form parameter
	my $content = $self->param('content');
	# Insert the title, content and a creation time into the articles collection
	$self->mango->db('test')->collection('articles')->insert({
		title => $title,
		content => $content,
		created => bson_time,
	} => sub {
		# Get the result
		my ($mango, $error) = @_;
		# Render an exception if we had an error
		$self->render_exception("Error: $error") if $error;
		# Otherwise redirect back to /
		$self->redirect_to('/');
	});
	# Render later so Mango query can run
	$self->render_later;
};

# Start our application
app->start;
__DATA__

@@ index.html.ep
% layout 'default';
% title 'Welcome';
Welcome to the Mojolicious real-time web framework!

@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
  <head>
	<title><%= title %></title>

	<!-- Latest compiled and minified CSS -->
	<link rel="stylesheet" href="//netdna.bootstrapcdn.com/bootstrap/3.1.0/css/bootstrap.min.css">
	<!-- Optional theme -->
	<link rel="stylesheet" href="//netdna.bootstrapcdn.com/bootstrap/3.1.0/css/bootstrap-theme.min.css">
	<!-- Latest compiled and minified JavaScript -->
	<script src="//netdna.bootstrapcdn.com/bootstrap/3.1.0/js/bootstrap.min.js"></script>
  </head>
  <body>
  <div class="container">
    <h1>My blog</h1>
    <h2>Post</h2>
    <form action="/post" method="post" class="form">
	<div class="form-group">
		<label>Title</label> <input type="text" name="title" class="form-control" />
	</div>
	<div class="form-group">
		<label>Content</label>
		<textarea name="content" class="form-control"></textarea>
	</div>
	<button type="submit" value="Post" class="btn btn-default">Post article</button>
    </form>
    <h2>Articles</h2>
    % for my $article (@$articles) {
	<div class="panel panel-default">
		<div class="panel-heading"><strong><%= $article->{title} %></strong></div>
		<div class="panel-body">
			<%= $article->{content} %>
		</div>
	</div>
    % }
   </div>
  </body>
</html>
